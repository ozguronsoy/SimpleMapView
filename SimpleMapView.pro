QT *= core network positioning

SIMPLE_MAP_VIEW_VERSION_MAJOR = 1
SIMPLE_MAP_VIEW_VERSION_MINOR = 2
SIMPLE_MAP_VIEW_VERSION_PATCH = 0
SIMPLE_MAP_VIEW_QML_URI = com.github.ozguronsoy.SimpleMapView

DEFINES += SIMPLE_MAP_VIEW_VERSION_MAJOR=$$SIMPLE_MAP_VIEW_VERSION_MAJOR
DEFINES += SIMPLE_MAP_VIEW_VERSION_MINOR=$$SIMPLE_MAP_VIEW_VERSION_MINOR
DEFINES += SIMPLE_MAP_VIEW_VERSION_PATCH=$$SIMPLE_MAP_VIEW_VERSION_PATCH

isEmpty(SIMPLE_MAP_VIEW_BUILD_QML) {

    QT *= gui widgets

} else {

    QT *= quick
    DEFINES += SIMPLE_MAP_VIEW_USE_QML

    QML_IMPORT_NAME = $$SIMPLE_MAP_VIEW_QML_URI
    QML_DESIGNER_IMPORT_PATH = $$SIMPLE_MAP_VIEW_QML_URI
    QML_IMPORT_MAJOR_VERSION = $$SIMPLE_MAP_VIEW_VERSION_MAJOR
}

INCLUDEPATH += $$PWD/include

HEADERS += $$files($$PWD/include/*.h) $$files($$PWD/include/SimpleMapView/*.h)
SOURCES += $$files($$PWD/src/*.cpp) $$files($$PWD/src/SimpleMapView/*.cpp)
