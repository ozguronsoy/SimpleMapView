cmake_minimum_required(VERSION 3.16)

if (CMAKE_CXX_STANDARD LESS 17)
	message(FATAL_ERROR "C++17 or higher is required")
endif()

set(SIMPLE_MAP_VIEW_VERSION_MAJOR 1)
set(SIMPLE_MAP_VIEW_VERSION_MINOR 1)
set(SIMPLE_MAP_VIEW_VERSION_PATCH 0)
set(SIMPLE_MAP_VIEW_VERSION "${SIMPLE_MAP_VIEW_VERSION_MAJOR}.${SIMPLE_MAP_VIEW_VERSION_MINOR}.${SIMPLE_MAP_VIEW_VERSION_PATCH}")
set(SIMPLE_MAP_VIEW_QML_URI "com.github.ozguronsoy.SimpleMapView")

project(SimpleMapView VERSION ${SIMPLE_MAP_VIEW_VERSION} LANGUAGES CXX)

set(SIMPLE_MAP_VIEW_DEFINITIONS
    SIMPLE_MAP_VIEW_VERSION_MAJOR=${SIMPLE_MAP_VIEW_VERSION_MAJOR}
    SIMPLE_MAP_VIEW_VERSION_MINOR=${SIMPLE_MAP_VIEW_VERSION_MINOR}
    SIMPLE_MAP_VIEW_VERSION_PATCH=${SIMPLE_MAP_VIEW_VERSION_PATCH}
)

set(SIMPLE_MAP_VIEW_QT_COMPONENTS Core Positioning Network)
set(SIMPLE_MAP_VIEW_QT_LIBRARIES Qt::Core Qt::Positioning Qt::Network)

if(DEFINED SIMPLE_MAP_VIEW_USE_QML)

    list(APPEND SIMPLE_MAP_VIEW_DEFINITIONS SIMPLE_MAP_VIEW_USE_QML SIMPLE_MAP_VIEW_QML_URI="${SIMPLE_MAP_VIEW_QML_URI}")
    list(APPEND SIMPLE_MAP_VIEW_QT_COMPONENTS Quick)
    list(APPEND SIMPLE_MAP_VIEW_QT_LIBRARIES Qt::Quick)

else()

    list(APPEND SIMPLE_MAP_VIEW_QT_COMPONENTS Gui Widgets)
    list(APPEND SIMPLE_MAP_VIEW_QT_LIBRARIES Qt::Gui Qt::Widgets)

endif()

find_package(Qt6 REQUIRED COMPONENTS ${SIMPLE_MAP_VIEW_QT_COMPONENTS})

file(GLOB_RECURSE SIMPLE_MAP_VIEW_SOURCES
	include/*.h
    src/*.cpp
)

add_library(SimpleMapView STATIC ${SIMPLE_MAP_VIEW_SOURCES})
target_compile_definitions(SimpleMapView PUBLIC ${SIMPLE_MAP_VIEW_DEFINITIONS})
target_include_directories(
    SimpleMapView
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/include>
)
target_link_libraries(SimpleMapView PUBLIC ${SIMPLE_MAP_VIEW_QT_LIBRARIES})