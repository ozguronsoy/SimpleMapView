QT *= core network positioning

SIMPLE_MAP_VIEW_VERSION_MAJOR = 1
SIMPLE_MAP_VIEW_VERSION_MINOR = 1
SIMPLE_MAP_VIEW_VERSION_PATCH = 0
SIMPLE_MAP_VIEW_QML_URI = com.github.ozguronsoy.SimpleMapView

DEFINES += SIMPLE_MAP_VIEW_VERSION_MAJOR=$$SIMPLE_MAP_VIEW_VERSION_MAJOR
DEFINES += SIMPLE_MAP_VIEW_VERSION_MINOR=$$SIMPLE_MAP_VIEW_VERSION_MINOR
DEFINES += SIMPLE_MAP_VIEW_VERSION_PATCH=$$SIMPLE_MAP_VIEW_VERSION_PATCH

isEmpty(SIMPLE_MAP_VIEW_USE_QML) {

    QT *= gui widgets

} else {

    QT *= quick
    DEFINES += SIMPLE_MAP_VIEW_USE_QML
    DEFINES += SIMPLE_MAP_VIEW_QML_URI=\\\"$$SIMPLE_MAP_VIEW_QML_URI\\\"

}

INCLUDEPATH += $$PWD/include

HEADERS += $$files($$PWD/include/*.h) $$files($$PWD/include/SimpleMapView/*.h)
SOURCES += $$files($$PWD/src/*.cpp) $$files($$PWD/src/SimpleMapView/*.cpp)

!isEmpty(SIMPLE_MAP_VIEW_ENABLE_RESOURCES) {

    RESOURCES += $$PWD/Resources.qrc

}

