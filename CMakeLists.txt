cmake_minimum_required(VERSION 3.16)

if (CMAKE_CXX_STANDARD LESS 17)
	message(FATAL_ERROR "C++17 or higher is required")
endif()

project(SimpleMapView VERSION 1.2.0 LANGUAGES CXX)

option(SIMPLE_MAP_VIEW_BUILD_QML "Build as QML component" OFF)
option(SIMPLE_MAP_VIEW_BUILD_PYTHON_BINDINGS "Build python bindings" OFF)

set(SIMPLE_MAP_VIEW_QML_URI "com.github.ozguronsoy.SimpleMapView")

set(SIMPLE_MAP_VIEW_DEFINITIONS
    SIMPLE_MAP_VIEW_VERSION_MAJOR=${PROJECT_VERSION_MAJOR}
    SIMPLE_MAP_VIEW_VERSION_MINOR=${PROJECT_VERSION_MINOR}
    SIMPLE_MAP_VIEW_VERSION_PATCH=${PROJECT_VERSION_PATCH}
)

set(SIMPLE_MAP_VIEW_QT_COMPONENTS Core Positioning Network)
set(SIMPLE_MAP_VIEW_QT_LIBRARIES Qt::Core Qt::Positioning Qt::Network)

if(SIMPLE_MAP_VIEW_BUILD_QML)

    list(APPEND SIMPLE_MAP_VIEW_DEFINITIONS SIMPLE_MAP_VIEW_USE_QML SIMPLE_MAP_VIEW_QML_URI="${SIMPLE_MAP_VIEW_QML_URI}")
    list(APPEND SIMPLE_MAP_VIEW_QT_COMPONENTS Quick)
    list(APPEND SIMPLE_MAP_VIEW_QT_LIBRARIES Qt::Quick)

else()

    list(APPEND SIMPLE_MAP_VIEW_QT_COMPONENTS Gui Widgets)
    list(APPEND SIMPLE_MAP_VIEW_QT_LIBRARIES Qt::Gui Qt::Widgets)

endif()

find_package(Qt6 REQUIRED COMPONENTS ${SIMPLE_MAP_VIEW_QT_COMPONENTS})

file(GLOB_RECURSE SIMPLE_MAP_VIEW_HEADERS include/*.h)
file(GLOB_RECURSE SIMPLE_MAP_VIEW_SOURCES src/*.cpp)

if(SIMPLE_MAP_VIEW_BUILD_PYTHON_BINDINGS)
    set(CMAKE_AUTOMOC ON)
    set(CMAKE_AUTORCC ON)
    set(CMAKE_AUTOUIC ON)
    list(APPEND SIMPLE_MAP_VIEW_SOURCES Resources.qrc)
    list(APPEND SIMPLE_MAP_VIEW_DEFINITIONS SIMPLE_MAP_VIEW_BUILD_PYTHON_BINDINGS)
endif()

add_library(SimpleMapView STATIC ${SIMPLE_MAP_VIEW_HEADERS} ${SIMPLE_MAP_VIEW_SOURCES})
target_compile_definitions(SimpleMapView PUBLIC ${SIMPLE_MAP_VIEW_DEFINITIONS})
target_include_directories(
    SimpleMapView
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/include>
)
target_link_libraries(SimpleMapView PUBLIC ${SIMPLE_MAP_VIEW_QT_LIBRARIES})

if(SIMPLE_MAP_VIEW_BUILD_PYTHON_BINDINGS)
    set_property(TARGET SimpleMapView PROPERTY POSITION_INDEPENDENT_CODE ON)
    add_subdirectory(python_bindings)
endif()